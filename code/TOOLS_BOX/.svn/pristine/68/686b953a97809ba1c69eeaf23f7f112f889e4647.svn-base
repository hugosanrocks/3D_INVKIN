!!$ Copyright (C) 2016-2018 Seiscope Consortium, All rights reserved.
!!$
!!$ Author: Pengliang Yang, Univ. Grenoble Alpes
!!$ Email: pengliang.yang@univ-grenoble-alpes.fr
!!$==========================================================================
!!$ References: 
!!$[1] Yang, P., Brossier, R., Metivier, L. and Virieux, J. (2016), Wavefield 
!!$   reconstruction in attenuating media: A Checkpointing-assisted 
!!$   reverse-forward simulation method, Geophysics,  Vol. 81, No. 6, 
!!$   pp. R349–R362
!!$[2]  Yang, P., Brossier, R., and Virieux, J. (2016). Wavefield reconstruction
!!$   from significantly decimated boundaries. Geophysics, Vol. 81, No. 5,
!!$   T197–T209
!!$==========================================================================
subroutine carfs_is_checkpoint(carfs,it,flag)
  implicit none
  include "carfs_type.h"

  type(carfs_type)::carfs
  integer :: it
  character(len=*)::flag

  carfs%icheckpoint=.false. !assume it is not checkpoint by default

  if(flag=='forward') then
     if(carfs%j==0) then
        if(carfs%ic>=0.and.carfs%ic<carfs%nc) then
           if(it==carfs%cpit(carfs%ic)) then
              carfs%icheckpoint=.true.
              carfs%icc=carfs%ic !output checkpoint index icc=ic
              carfs%ic=carfs%ic+1 !absolute index of the checkpoint
           endif
        endif
     elseif(carfs%j>0) then
        if(carfs%ic+carfs%j>=0.and.carfs%ic+carfs%j<carfs%nc) then
           if(it==carfs%cpit(carfs%ic+carfs%j)) then
              carfs%icheckpoint=.true.
              carfs%icc=carfs%ic+carfs%j !output checkpoint index icc=ic+j
              carfs%j=carfs%j+1 !relative index of the checkpoint associated with ic
           endif
        endif
     endif
  elseif(flag=='backward') then
     !find the closesest checkpoint prior to current time level-it
     carfs%ic=carfs%nc-1
     do while(carfs%ic>=0 .and. it<carfs%cpit(carfs%ic))
        carfs%ic=carfs%ic-1
     enddo
     carfs%icc=carfs%ic !output checkpoint index icc=ic
     if(it==carfs%cpit(carfs%ic)) carfs%icheckpoint=.true.
  endif
end subroutine carfs_is_checkpoint
