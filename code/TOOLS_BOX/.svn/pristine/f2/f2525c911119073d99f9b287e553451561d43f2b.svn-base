!!$  Copyright (C) 2015 Seiscope- University Grenoble Alpes (Pengliang Yang)
!!$			ypl.2100@gmail.com; pengliang.yang@ujf-grenoble.fr
!!$ References: 
!!$[1] P. Yang and R. Brossier and L. M\'etivier and J. Virieux, Checkpointing
!!$    assisted Reverse Forward Simulation: an optimal recomputation method for 
!!$    FWI and RTM, Expanded Abstracts, 78$^{th}$ Annual EAGE Meeting (Vienna),
!!$    2016
!!$[2] Yang, P., Brossier, R., and Virieux, J. (2016b). Wavefield reconstruction
!!$    from significantly decimated boundaries. Geophysics, 2016 page submitted.
!!$[3] Yang, P., Gao, J., and Wang, B. (2014a). RTM using effective boundary 
!!$    saving: A staggered grid GPU implementation. Computers & Geosciences,
!!$    68:64–72.
!!$[4] Yang, P., Gao, J., and Wang, B. (2015). A graphics processing unit 
!!$    implementation of time-domain full-waveform inversion. Geophysics, 
!!$    80(3):F31–F39.
!!$[5] Yang, P., Wang, B., and Gao, J. (2014b). Using the effective boundary 
!!$    saving strategy in GPU-based RTM programming. In SEG technical program 
!!$    expanded abstracts 2014. Society of Exploration Geophysicists.
!-----------------------------------------------------------------!
! part i: discrite fourier interpolation for boundary values
!-----------------------------------------------------------------!
subroutine dft_interp(v2b,var,var0,i1min,i1max,i2min,i2max,i3min,i3max,nt,mt,it)
  !interpolate the boundary elements by Kaiser window sinc interpolant
  implicit none

  logical::v2b !v to bourndary or reverse
  integer::i1min !minimum index of boundary elements for 1-axis 
  integer::i1max !maximum index of boundary elements for 1-axis 
  integer::i2min !minimum index of boundary elements for 2-axis 
  integer::i2max !maximum index of boundary elements for 2-axis 
  integer::i3min !minimum index of boundary elements for 3-axis 
  integer::i3max !maximum index of boundary elements for 3-axis 
  integer::nt !number of time steps
  integer::mt,it
  complex::var(mt/2+1,i1min:i1max,i2min:i2max,i3min:i3max)
  real::var0(i1min:i1max,i2min:i2max,i3min:i3max)
  real,parameter::PI=3.14159265359

  integer::r,i1,i2,i3,ik
  complex::factor(mt/2+1)
  r=nt/mt

  if(v2b) then !v to bourndary
     !"it" originates from 1 rather than 0
     !assume it originates from 1, n=it-1 starts from 0
     if(mod(it-1,r)==r-1) then !only every r-step, store fourier coefficients
        do ik=1,mt/2+1
           !complex exponential factor
           factor(ik)=exp(cmplx(0,-2.*pi*(ik-1)*(it-1)/nt))
        enddo
        do i3=i3min,i3max
           do i2=i2min,i2max
              do i1=i1min,i1max
                 var(:,i1,i2,i3)=var(:,i1,i2,i3)+var0(i1,i2,i3)*factor
              enddo
           enddo
        enddo
     endif
  else !boundary to v, this part is done at every backward step
     do ik=1,mt/2+1
        factor(ik)=exp(cmplx(0,2.*pi*(ik-1)*(it-1)/nt))/mt
     enddo
     factor(1)=0.5*factor(1)
     !handle the case mt is an even number
     if(mod(mt,2)==0) factor(mt/2+1)=0.5*factor(mt/2+1)
     do i3=i3min,i3max
        do i2=i2min,i2max
           do i1=i1min,i1max
              var0(i1,i2,i3)=2.*real(sum(var(:,i1,i2,i3)*factor))
           enddo
        enddo
     enddo
  endif
end subroutine dft_interp
